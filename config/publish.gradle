apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.github.lukasdylan'
version = generateVersionName()

android {
    defaultConfig {
        versionName generateVersionName()
        versionCode generateVersionCode()
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                credentials {
                    username "$nexusUsername"
                    password "$nexusPassword"
                }
                def releasesRepoUrl = "${nexusUrl}/content/repositories/releases/"
                def snapshotsRepoUrl = "${nexusUrl}/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                name = 'protodroid'
            }
        }
        publications {
            def artifactName = 'Protodroid'
            def artifactDescription = 'Protodroid is an Android library for intercept every API request and response on GRPC'
            def artifactUrl = 'https://github.com/lukasdylan/protodroid'

            // Creates a Maven publication called "release".
            release(MavenPublication) {
                pom {
                    name = artifactName
                    description = artifactDescription
                    url = artifactUrl
                }

                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'io.github.lukasdylan'
                artifactId = 'protodroid-no-op'
                version = generateVersionName()
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                pom {
                    name = artifactName
                    description = artifactDescription
                    url = artifactUrl
                }

                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'io.github.lukasdylan'
                artifactId = 'protodroid'
                version = generateVersionName()
            }
        }
    }
}

private String generateVersionName() {
    def name = rootProject.appVersionString
    if (project.hasProperty("versionName")) {
        name = project.versionName
    }
    return name
}

private Integer generateVersionCode() {
    def code = rootProject.appVersionCode
    if (project.hasProperty("versionCode")) {
        code = Integer.parseInt(versionCode)
    }
    return code
}
